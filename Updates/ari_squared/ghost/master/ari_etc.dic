//---------------------------Miscellaneous Functions--------------------------
/*
This file has...

OnInstallBegin
OnInstallComplete
OnInstallFailure
OnInstallRefuse
OnVanishSelecting
OnVanishSelected
OnVanishCancel
OnVanishButtonHold
OnVanished
OnBIFFBegin
OnBIFFComplete
OnBIFFFailure
OnUpdateBegin
OnUpdateReady
OnUpdateComplete
OnUpdateFailure
OnUpdate.OnDownloadBegin
OnUpdate.OnMD5CompareBegin
OnUpdate.OnMD5CompareComplete
OnUpdate.OnMD5CompareFailure
OnHeadlinesenseBegin
OnHeadlinesense.OnFind
Select.Headline_Cancel
OnHeadlinesenseComplete
OnHeadlinesenseFailure
OnUpdatedataCreating
OnUpdatedataCreated
OnNarCreating
OnNarCreated
OnSNTPBegin
OnSNTPCompare
OnSNTPEXECUTE
OnSNTPCANCEL
OnSNTPFailure
OnDisplayChange
OnScreenSaverStart
OnScreenSaverEnd
OnApplicationBoot
OnApplicationClose
OnApplicationFileOpen
OnBatteryLow
OnBatteryCritical
OnShellScaling
OnFileDrop2
*/

 //---- OnInstallBegin -------------------------------------------------------

OnInstallBegin
 {
	"\0\s[0]Oh, \w4hey!\w8 You're installing something?\w8\s[5] Cool!\w8\s[8] Let's see what we got here.\w8.\w8.\w8\e"
 }

 //---- OnInstallComplete -------------------------------------------------------
 
OnInstallComplete
 {
	"\0"
 	--
	if reference0 == "shell" 
 	{	
		if reference1 == "Mumbo" 
		{
			"\0\s[7]Mumbo.\w8.\w8.\w8? \w8\s[8]Weird.\w8\s[3] I strangely feel like some part of me has turned british.\e"
		}
		elseif reference1 == "Ari Fire Flavor"
		{
			"\0\s[0]Oh neat!\w8 I love fire.\w8\s[5] Now I can be on fire! \w8\s[6]Truely the most gender i can be.\e"
		}
		else 
		{
			"\0\s[0]Ooo,\w4 a new shell!\w8\s[8] I wonder what it is.\w8.\w8.\w8%(reference1).\w8.\w8\.\w8\s[3] I hope it looks nice!\e"
		}
 	}
	elseif reference0 == "ghost"
 	{
		//might make them comment on specific ghosts if I use them often? -Ari
		"\s[0]Oh,\w4 Fun!\w8\s[3] A ghost I haven't seen much of before!\w8\s[6] It's called %(reference1).\w8.\w8.\w8\s[8] Sounds really interesting.\w8\s[0] Do you wanna try them out now?\w8\n\n"
		--
		"\q[Change to %(reference1),ChangeToGhost]\n/
		\q[Call %(reference1),CallGhost]\n/
		\q[No thanks,CancelChange]\e"
		
 	}
	elseif reference0 == "balloon" 
 	{
		"\s[0]Installed a new balloon. It's called %(reference1).\e"
		
 	}
	elseif reference0 == "plugin"
 	{
		"\s[0]Installed a plug-in. It's called %(reference1).\e"
		
 	}
	elseif reference0 == "calendar skin" 
	{
		"\s[0]Installed a calendar skin. It's called %(reference1)."
	}
	elseif reference0 == "headline" 
	{
		"\0\s[0]Set up a headline. It's called %(reference1).\e"
	}
	elseif reference0 == "rss" 
	{
		"\0\s[0]Set up a headline. It's called %(reference1).\e"
	}
	else 
	{
		"\s[0]Installed %(reference1)."
	}
}

Select.ChangeToGhost
{
	"\![change,ghost,lastinstalled]"
}

Select.CallGhost
{
	"\![call,ghost,lastinstalled]"
}

Select.CancelChange
{
	"\0\s[0]Darn.\w8 I was excited to see someone new.\w8 I guess I'll just meet them in your files after you close me,\w4 haha!\e"
}

//---- OnInstallFailure -------------------------------------------------------

OnInstallFailure
 {
	"\0\s[0]The installation has failed.\n\n[half]\w5" 
	--
	if reference0 == "unsupported" 
	{
	"This file is unsupported.\e"
	}	
	elseif reference0 == "extraction" 
	{
	"The file could not be extracted properly.\e"
	}
 }
 
 //--OnInstallRefuse
 
OnInstallRefuse
 {
	"\0\s[0]Can't install this. \w4It may be for %(reference0) instead?\e"
 }


//**** Uninstallation ******************************************

//--OnVanishSelecting

OnVanishSelecting
{
	"\0\s[0]Waugh, You wanna uninstall me? Uhm- If you're just looking around, PLEASE be careful!\e"
}

//--OnVanishSelected


OnVanishSelected
{
	"\0\s[0]Oh, uh... Well, I'm sure you have good reasons. Bye, %(names)!\w9\w9\e"
}

//--OnVanishCancel

OnVanishCancel
{
	"\0\s[0]Oh, good! Glad You let me stay!\e"
}

//--OnVanishButtonHold

OnVanishButtonHold
{
	"\0\s[0]Oh, so you're- Okay, you're gonna leave me be for now? okay, cool.\e"
}


 //---- OnVanished -----------------------------------------
  
OnVanished
 {
	"\0\s[0]\1\s[10]\0\s[0]%(reference0) has been uninstalled.\w8 You, uh... Won't do that to me, right?\e"
	
	
}


//**** Mail Checking *******************************************************************


 //---- OnBIFFBegin ---------------------------------------------------------------
 
OnBIFFBegin
 {
	"\0\s[0]Checking the emaiiilll!\e"
	
	"\0\s[0]Honestly, I'm surprised anyone uses this function. Oh well, Email time!\e"
 }

 
 //---- OnBIFFComplete ---------------------------------------------------------------

 
OnBIFFComplete
 {
	if reference0 == 0 
 	{
 		"\0\s[0]Nothing new.\e"
	}
	elseif reference0 == 1 
 	{
 		"\0\s[0]Ya got an email!\e"
	}
	elseif reference0 > 1 && reference0 < 11 
	{
		"\0\s[0]You have %(reference0) new emails!\e"
	}
	elseif reference0 > 100 
	{
		"\0\s[0]%(reference0) emails unread...\w8 Are you insane?\e"
	}
 	else 
	{
		"\0\s[0]There's %(reference0) emails!\e"
 	}
}

 //---- OnBIFFFailure ---------------------------------------------------------------
 
OnBIFFFailure
 {
	if reference0 == "timeout" 
 	{
 		"\0\s[0]Timed out... Darn. Try again later?\e"
	}
 	elseif reference0 == "kick" 
	{
		"\0\s[0]Oh, damn! I can't access your email...\e"
 	}
	elseif reference0 == "defect" 
 	{
 		"\0\s[0]Oh, it looks like your email wasn't set up properly! How about you fix that?\w8\![open,configurationdialog]\e"
	}
 	else 
	{
		"\0\s[0]I can't connect to the account, I got an error: '%(reference[0])'.\e"
 	}
}


 //**** Network Update *****************************************************************
 

 //---- OnUpdateBegin -------------------------------------------------------------
 
 
OnUpdateBegin
 {
	"\0\s[0]Alriiigghht, let me get connected to the servers here...\w8\e"
	
	"\0\s[0]Updating, updating, we gotta update, woo!\w8\e"
 }

 //---- OnUpdateReady ---------------------------------------------------------

 
OnUpdateReady
 {
 
	reference0++ 
	
    "\0\s[0]There are %(reference0) new files! Let's get loading!\w8\e"
	

 }


//---- OnUpdateComplete -------------------------------------------------------------

OnUpdateComplete
 {
	if reference0 == "none" 
 	{
 		"\0\s[0]Nothing new!\e"
		
		"\0\s[0]You sure there was an update? Because I got nothin'.\e"
	}
 	else 
	{
		"\0\s[0]Update successful!\e"
 	}
}

 //---- OnUpdateFailure -------------------------------------------------------------

 
OnUpdateFailure
 {
	 _file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] 
	 
	 
	if reference0 == "timeout" 
 	{
 		"\0\s[0]The update connection has timed out... darn.\e"
	}
 	elseif reference0 == "md5 miss" 
	{
		"\0\s[0]The MD5 hash of %(_file) doesn't match! Tell the dev!\e"
 	}
	else 
 	{
 		"\0\s[0]Got an error! Couldn't update properly, augh... The error is '%(reference0)'.\e"
	}
 }


 //---- OnUpdate.OnDownloadBegin ------------------------------------------------.
 
 
//---- Downloading the update ------------------------------------------------
OnUpdate.OnDownloadBegin
 {
	 "\_q\b[2]Downloading %(reference0)...\n\n"
		 --
		 
		Update_Process = TOINT(reference1) + 1
		Update_Total = TOINT(reference2) + 1
		Update_Remaining = Update_Total - Update_Process
		
		PointA = 1 
		PointB = 1
		
		_txt = ""
		
		while PointA <= Update_Process 
		{
			_txt += "☑" 
			PointA++ 
		} 
		while PointB <= Update_Remaining 
		{
			_txt += "☐" 
			PointB++ 
		}
		--
		"%(_txt)\n\n%(Update_Process) of %(Update_Total)"	
		--
		"\w5\e"
 }

 //---- OnUpdate.OnMD5CompareBegin ---------------------------------------------------

 
OnUpdate.OnMD5CompareBegin
 {
	//"\1\s[10]Checking the MD5 value.\e"
 }

 //---- OnUpdate.OnMD5CompareComplete --------------------------------------------------
 
OnUpdate.OnMD5CompareComplete
 {
	//"\0\s[0]MD5 check good.\e"
}

 //---- OnUpdate.OnMD5CompareFailure ---------------------------------------------------
 
OnUpdate.OnMD5CompareFailure
 {
	"\0\s[0]The MD5 values don't match... Weird. Better tell the dev.\w8\e"
 }

 
 
 //**** Headlines and RSS Feeds *********************************************************

 //---- OnHeadlinesenseBegin -----------------------------------------------------------
 
 
OnHeadlinesenseBegin
 {
	"\0\s[0]You've selected %(reference0).\e"
 }

 //---- OnHeadlinesense.OnFind --------------------------------------------------------
 
OnHeadlinesense.OnFind
{

 	if reference2 == "First" || reference2 == "First and Last"
	{
		"\0\b2\s[0]%(reference0):"
 	}
	elseif reference2 == "Next"
 	{
 		"\0\b2\s[0]%(reference0):"
	}
 	elseif reference2 == "Last"
	{
		"\0\b2\s[0]%(reference0):"
 	}
	--
	"\n\n%reference3\n"
 	--
 	if reference2 == "First" || reference2 == "Next"
	{
		"\![*]\q[Next,] - \![*]\q[Stop,Headline_Cancel]\e"
 	}
	else
 	{
 		"\![*]\q[Stop,Headline_Cancel]\e"
 	}
	
}


Select.Headline_Cancel
{
	"\0\s[0]Headline viewing complete.\e"
}

 //---- OnHeadlinesenseComplete -------------------------------------------------------
 
OnHeadlinesenseComplete
 {
	"\0\s[0]No update. %(reference0).\e"
 }

 //---- OnHeadlinesenseFailure ---------------------------------------------------------
 
OnHeadlinesenseFailure
  {
	"\0\s[0]The feed can't updated.\w8\1\s[10]Why not?\w8\0\s[0]\n\n[half]" 
 	--
	if reference0 == "can't download" 
 	{
		"It can't be downloaded."
 	}
	elseif reference0 == "can't analyze" 
 	{
		"It can't be analyzed."
 	}
	else 
 	{
		"I don't know."
 	}
	--
 	"\e" 
}


//**** Ghost Creation and Updating ******************************************************


 //---- OnUpdatedataCreating -----------------------------------------------------------
 
OnUpdatedataCreating
 {
	"\0\s[0]Making that updates2.dau file!\e"
 }

 //---- OnUpdatedataCreated -------------------------------------------------------------

OnUpdatedataCreated
 {
	"\0\s[0]Did it! the file updates2.dau was made!\e"
 }

 //---- OnNarCreating ------------------------------------------------------------------

 
OnNarCreating
 {
	"\0\s[0]Creating the .nar file...\e"
 }

 //---- OnNarCreated -------------------------------------------------------------------

 
OnNarCreated
 {
	"\0\s[0]Successfully created '%(reference0)'!\e"
 }

 //****　Clock Synchronization　**********************************************************
//**** 미카 (http://homepage2.nifty.com/ko-ki/)의 사전을 참고했습니다．감사＞こ 나무 성
//time sync code by above creator :D -Ari
 

 //----OnSNTPBegin ------------------------------------------------------------------
 
OnSNTPBegin
 {
	"\0\s[0]Checking the time...\e"
 }


 //----　OnSNTPCompare----------------------------------------------------------
 
OnSNTPCompare
 {
 	"Retrieved Time: %reference1[1]/%reference1[2]/%reference1[0] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	Your Time: %reference2[1]/%reference2[2]/%reference2[0] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
 	\n"
	--
 	if reference3 == 0
	{
 		"\0\s[0]Looks like your clock is all good!\e"
	}
 	else 
	{
 		"\0\s[0]Aw, crap... your clock doesn't match. \w6\nShould I fix that?\n\n[half]/
		\![*]\q[Fix it,OnSNTPEXECUTE]\n/
 		\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
	}
 }

 //----　OnSNTPEXECUTE ------------------------------------------------------------------
 
OnSNTPEXECUTE
 {
	"\0\s[0]Done! Now the time should be accurate!\6\e"
 }

 //----　OnSNTPCANCEL ------------------------------------------------------------------
 
OnSNTPCANCEL
 {
	"\0\s[0]Oh, you're good as is? Okay, cool!\e"
 }

 //----　OnSNTPFailure ------------------------------------------------------------------
 
OnSNTPFailure
 {
	"\0\s[0]Oop... Something went wrong. I couldn't change your clock.\e"
 }


 //--OnDisplayChange----------------------------------------------------------------
 
OnDisplayChange
 {
	displaybpp    = reference0
 	displaywidth  = reference1
	displayheight = reference2
 }



 //---- OnScreenSaverStart -------------------------------------------------------
 
OnScreenSaverStart
 {
	"\0\s[0]Oh cool, the screensaver's coming on!\e"

 }

 //---- OnScreenSaverEnd -------------------------------------------------------
 
OnScreenSaverEnd
 {
	"\0\s[0]Awe man, I wanted to keep watching that screensaver you had on...\e"

 }


 //---- OnApplicationBoot ---------------------------------------------------------------
 
OnApplicationBoot
 {
	"\0\s[0]%(reference0) has started up!\e"
 }

 //---- OnApplicationClose --------------------------------------------------------------

 
OnApplicationClose
 {
	"\0\s[0]%(reference0) has closed.\e"
 }

 //---- OnApplicationFileOpen ----------------------------------------------------------

 
OnApplicationFileOpen
 {
	"\0\s[0]Opening %(reference1)!\e"
 }




 //---- OnBatteryLow -----------------------------------------------------------------
 
OnBatteryLow
 {
	"\0\s[0]Hey! Your laptop's about to run out of battery, you should plug it in! The battery's at %(reference0) percent!\e"
 }
 
 //--OnBatteryCritical-----------------------------------------------------------------
 
OnBatteryCritical
{
	"\0\s[0]PLUG YOUR LAPTOP IN!!!!\e"
}
 
//--OnBatteryNotify--------------------------------------------------------------------


OnBatteryNotify
{
	if reference2 == "online" //If your laptop is plugged in
	{
		"\0\s[0]The laptop is plugged in! Good!\e"
	}
	elseif reference2 == "offline" //If you unplug your laptop
	{
		"\0\s[0]Ah, you unplugged your laptop. Watch that battery!\e"
	}
}



 //--OnShellScaling
 
OnShellScaling
{
	if reference0 >= 150 
	{
		"\0\s[0]Embiggened!\e"
	}
	elseif reference0 == 100 
	{
		"\0\s[0]Normal size!\e"
	}
	elseif reference0 == 50 
	{
		"\0\s[0]Shrunk. Just a little guy. Mini.\e"
	}
}


//--OnFileDrop2



OnFileDrop2
{
	_filepath = SPLITPATH(reference0)
	_filename = _filepath[2]
	_fileext = TOLOWER(_filepath[3])
	case _fileext
	{
		when ".jpg", ".jpeg", ".bmp", ".png" 
		{
			Wallpaperfile = reference0
			
			"\0\s[0]The file you dropped is %(_filename)%(_fileext)... Oh, I can set it as a wallpaper! How would you like me to do that?\n\n[half]/
			\![*]\q[Centered,SetWallpaper_0]\n/
			\![*]\q[Tiled,SetWallpaper_1]\n/
			\![*]\q[Stretched,SetWallpaper_2]\n\n[half]/
			\![*]\q[No thanks,SetWallpaper_Cancel]\e"
		}
		when ".mid", ".wav", ".mp3", ".wma", ".au" 
		{
			"\0\s[0]%(_filename)%(_fileext)... Oh, it's a music file! I can't do anything with that, but someone else might be able to!\e"
		}
		others 
		{
			"\0\s[0]%(_filename)%(_fileext)... I can't do anything with this, but thanks for showing it to me!!\e"
		}
	}
}

Select.SetWallpaper_0
{
	"\![set,wallpaper,%(Wallpaperfile),center]\0\s[0]The image is centered!\e"
}
Select.SetWallpaper_1
{
	"\![set,wallpaper,%(Wallpaperfile),tile]\0\s[0]The image is tiled!\e"
}
Select.SetWallpaper_2
{
	"\![set,wallpaper,%(Wallpaperfile),stretch]\0\s[0]The image is stretched!\e"
}
Select.SetWallpaper_Cancel
{
	"\0\s[0]Oh, Ya don't want it as your wallpaper? Alright!\e"
}